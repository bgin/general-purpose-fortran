NAME
   rect(3f) - [M_DRAW:POLYGONS] Draw a rectangle given two corners

SYNOPSIS
      subroutine rect(x1, y1, x2, y2)
      real,intent(in) :: x1,y1,x2,y2

DESCRIPTION
   Draw rectangle given two opposite corners.

   Note: rectangles are regarded as polygons, so if
   polyfill or polyhatch has been called with .TRUE., the rectangle will
   be filled or hatched accordingly.

OPTIONS
  Given

      x1,y1 ############ x2,y1
            #          #
            #          #
            #          #
      x1,y2 ############ x2,y2

   X1,Y1  coordinates of a corner of the rectangle
   X2,Y2  coordinates of corner point opposite first point

EXAMPLE
  Sample program:

   program demo_rect
   use M_draw
   implicit none
   integer :: i, ipaws
   real    :: r

   !! set up graphics area
   call prefsize(400,400)
   call vinit(' ') ! start graphics using device $M_DRAW_DEVICE
   call ortho2(left=-100.0, right=100.0, bottom=-100.0, top=100.0)

   !! make sure display is ready, which can be a problem with X11
   call vflush()
   ipaws=getkey()

   !! draw some filled rectangles
   call polyfill(.true.)
   do i=95,5,-10
      call color(i/10)
      r=real(i)
      call rect( -1.0*r, -1.0*r, 1.0*r, 1.0*r )
   enddo

   !! draw some rectangles
   call polyfill(.false.)
   call linewidth(50)
   call color(7)
   do i=5,95,5
      r=real(i)
      call rect( -1.0*r, -1.0*r, 1.0*r, 1.0*r )
   enddo

   !! pause
   call vflush()
   ipaws=getkey()

   !! wrap up graphics
   call vexit()

   end program demo_rect

